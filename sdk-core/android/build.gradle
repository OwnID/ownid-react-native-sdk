buildscript {
    repositories {
        google()
        mavenCentral()
    }
    dependencies {
        classpath("com.android.tools.build:gradle:8.1.1")
        classpath("org.jetbrains.kotlin:kotlin-gradle-plugin:1.8.22")
        classpath("com.facebook.react:react-native-gradle-plugin")
    }
}

plugins {
    id("com.android.library")
    id("org.jetbrains.kotlin.android")
    id("com.facebook.react")
}

ext {
    name = "OwnID React Native Core SDK"
    groupId = "com.ownid.android-sdk"
    artifactId = "react-native-core"
    versionName = "3.6.1"
}

group = ext.groupId

android {
    namespace = "com.ownid.sdk.reactnative"
    compileSdk = 34
    buildToolsVersion = "34.0.0"

    defaultConfig {
        minSdk = 23
        targetSdk = 34

        gradle.projectsEvaluated {
            preBuild.dependsOn(setVersionProperties)
        }
    }

    kotlinOptions {
        freeCompilerArgs += ["-module-name", "com.ownid.sdk.reactnative.core"]
        freeCompilerArgs += "-Xexplicit-api=strict"
    }

    lint {
        enable "Interoperability"
    }
}

repositories {
    def found = false
    def defaultDir = null

    if (rootProject.ext.has("reactNativeAndroidRoot")) {
        defaultDir = rootProject.ext.get("reactNativeAndroidRoot")
    } else {
        defaultDir = new File(projectDir, "/../../../node_modules/react-native/android")
    }

    if (defaultDir.exists()) {
        maven { url defaultDir.toString() }
        found = true
    } else {
        def parentDir = rootProject.projectDir

        1.upto(5, {
            if (found) return true
            parentDir = parentDir.parentFile

            def androidSourcesDir = new File(parentDir, "node_modules/react-native")
            def androidPrebuiltBinaryDir = new File(parentDir, "node_modules/react-native/android")

            if (androidPrebuiltBinaryDir.exists()) {
                maven { url androidPrebuiltBinaryDir.toString() }
                found = true
            } else if (androidSourcesDir.exists()) {
                maven { url androidSourcesDir.toString() }
                found = true
            }
        })
    }

    if (!found) {
        throw new GradleException(
                "${project.name}: unable to locate React Native android sources. " +
                        "Ensure you have you installed React Native as a dependency in your project and try again."
        )
    }

    mavenCentral { content { excludeGroup "com.facebook.react" } }
    google()
}

dependencies {
    // The version of react-native is set by the React Native Gradle Plugin
    api("com.facebook.react:react-android")
    api("com.ownid.android-sdk:core:3.6.1")
}

def propertiesFileName = "src/main/assets/com/ownid/sdk/react-native-core.properties"
clean { delete propertiesFileName }
tasks.register("setVersionProperties", WriteProperties) {
    outputFile file(propertiesFileName)
    property "name", "OwnIDReactNativeCore"
    property "version", project.ext.versionName
}