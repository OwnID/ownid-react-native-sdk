buildscript {
    repositories {
        google()
        mavenCentral()
    }
    dependencies {
        classpath("com.android.tools.build:gradle:8.2.2")
        classpath("org.jetbrains.kotlin:kotlin-gradle-plugin:1.8.22")
        classpath("com.facebook.react:react-native-gradle-plugin")
    }
}

plugins {
    id("com.android.library")
    id("org.jetbrains.kotlin.android")
    id("com.facebook.react")
}

ext {
    name = "OwnID React Native Gigya SDK"
    groupId = "com.ownid.android-sdk"
    artifactId = "react-native-gigya"
    versionName = "3.3.0"
}

group = ext.groupId

android {
    namespace = "com.ownid.sdk.reactnative.gigya"
    compileSdk = 34
    buildToolsVersion = "34.0.0"

    defaultConfig {
        minSdk = 23
        targetSdk = 34

        gradle.projectsEvaluated {
            preBuild.dependsOn(setVersionProperties)
        }
    }

    kotlinOptions {
        freeCompilerArgs += ["-module-name", "com.ownid.sdk.reactnative.gigya"]
        freeCompilerArgs += "-Xexplicit-api=strict"
    }

    lint {
        enable "Interoperability"
    }
}

repositories {
    def found = false
    def defaultDir = null

    if (rootProject.ext.has("reactNativeAndroidRoot")) {
        defaultDir = rootProject.ext.get("reactNativeAndroidRoot")
    } else {
        defaultDir = new File(projectDir, "/../../../node_modules/react-native/android")
    }

    if (defaultDir.exists()) {
        maven { url defaultDir.toString() }
        found = true
    } else {
        def parentDir = rootProject.projectDir

        1.upto(5, {
            if (found) return true
            parentDir = parentDir.parentFile

            def androidSourcesDir = new File(parentDir, "node_modules/react-native")
            def androidPrebuiltBinaryDir = new File(parentDir, "node_modules/react-native/android")

            if (androidPrebuiltBinaryDir.exists()) {
                maven { url androidPrebuiltBinaryDir.toString() }
                found = true
            } else if (androidSourcesDir.exists()) {
                maven { url androidSourcesDir.toString() }
                found = true
            }
        })
    }

    if (!found) {
        throw new GradleException(
                "${project.name}: unable to locate React Native android sources. " +
                        "Ensure you have you installed React Native as a dependency in your project and try again."
        )
    }

    mavenCentral { content { excludeGroup "com.facebook.react" } }
    google()
}

dependencies {
    api(project(":ownid_react-native-core"))

    api("com.ownid.android-sdk:gigya:3.3.0")
    compileOnly("com.sap.oss.gigya-android-sdk:sdk-core:7.0.10")
}

def propertiesFileName = "src/main/assets/com/ownid/sdk/react-native-gigya.properties"
clean { delete propertiesFileName }
tasks.register('setVersionProperties', WriteProperties) {
    outputFile file(propertiesFileName)
    property "name", "OwnIDReactNativeGigya"
    property "version", project.ext.versionName
}